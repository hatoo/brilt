(datatype Bool
  (True)
  (False)
)

(datatype OptionI
  (SomeI i64)
  (NoneI)
)

(datatype Expr
  (ConstInt i64 :cost 0)
  (ConstBool Bool :cost 0)
  (Arg i64)
  (Add Expr Expr)
  (Sub Expr Expr)
  (Gt Expr Expr)
  (Eq Expr Expr)
)

(datatype ListI
  (ConsI i64 ListI)
  (NilI)
)

(datatype StateExpr
  (SArg i64)
  (Call ListI)
  (Return OptionI)
  (Print i64)
)

(datatype ListE
  (ConsE i64 Expr ListE)
  (NilE)
)

(datatype ListS
  (ConsS i64 StateExpr ListS)
  (NilS)
)

(datatype ListII
  (ConsII i64 i64 ListII)
  (NilII)
)

(datatype OptionS
  (SomeS StateExpr)
  (NoneS)
)

(datatype Node
  (Simple ListE)
  (StateFul ListS OptionS)
  (BranchIf i64 Node Node)
  (Loop i64 Node ListII)
)

(datatype List
  (Cons Node List)
  (Nil)
)

(function Linear (List) Node)

; Start Rules

(datatype Value
  (Int i64)
  (Boolean Bool)
)

(function ConstValue (Node i64) Value)

(rewrite
  (Cons node (Cons (BranchIf i then else) rest))
  (Cons node (Cons then rest))
  :when ((= (ConstValue node i) (Boolean (True))))
)

(rule
  ((Cons node (Cons (BranchIf i then else) rest)))
  ((ConstValue node i))
)

(rewrite
  (ConstValue (Simple (ConsE i (ConstBool b) rest)) i)
  (Boolean b)
)

(rewrite
  (ConstValue (Simple (ConsE i (ConstInt x) rest)) i)
  (Int x)
)

(rewrite
  (ConstValue (Simple (ConsE i _expr rest)) j)
  (ConstValue (Simple rest) j)
  :when ((< i j))
)

(relation Foreach (Node ListE))

(rule
  ((Foreach node (ConsE x (Arg i) rest)))
  (
    (ConstValue node i)
    (Foreach node rest)
  )
)

(rule
  ((Cons node (Cons (Simple list) rest)))
  ((Foreach node list))
)

(function rec_list (Node ListE) ListE)

(function value-to-expr (Value) Expr)

(rewrite
  (value-to-expr (Int x))
  (ConstInt x)
)

(rewrite
  (value-to-expr (Boolean b))
  (ConstBool b)
)

(rewrite
  (rec_list node (ConsE i (Arg x) rest))
  (ConsE i (value-to-expr (ConstValue node x)) (rec_list node rest))
)

(rewrite
  (rec_list node (ConsE i expr rest))
  (ConsE i expr (rec_list node rest))
)

(rewrite
  (rec_list node (NilE))
  (NilE)
)

(rewrite
  (Cons node (Cons (Simple list) rest))
  (Cons node (Cons (Simple (rec_list node list)) rest))
)

; https://github.com/sampsyo/bril/blob/main/examples/test/df/cond.bril
;@main {
;  a: int = const 47;
;  b: int = const 42;
;  cond: bool = const true;
;  br cond .left .right;
;.left:
;  b: int = const 1;
;  c: int = const 5;
;  jmp .end;
;.right:
;  a: int = const 2;
;  c: int = const 10;
;  jmp .end;
;.end:
;  d: int = sub a c;
;  print d;
;}

; cond is a constant. So egglog will optimize branches.
; (let expr
; (Linear (Cons (Simple (ConsE 0 (ConstInt 47) (ConsE 1 (ConstBool (True)) (NilE)))) (Cons (BranchIf 1 (Simple (ConsE 0 (Arg 0) (ConsE 1 (ConstInt 5) (NilE)))) (Simple (ConsE 0 (ConstInt 2) (ConsE 1 (ConstInt 10) (NilE))))) (Cons (Linear (Cons (Simple (ConsE 0 (Sub (Arg 0) (Arg 1)) (NilE))) (Cons (StateFul (NilS) (SomeS (Print 0))) (Cons (StateFul (NilS) (SomeS (Return (NoneI)))) (Nil))))) (Nil)))))
; )


; (run 100)

; (print-table ConstValue)
; (extract expr)
